apply plugin: 'java'
apply plugin: 'checkstyle'

repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
}

configurations {
    buildToolsConfig
}

dependencies {
    buildToolsConfig 'org.trellisldp:trellis-build-tools:0.2.0'
}

task processBuildTools(type: Copy) {
    from {
        configurations.buildToolsConfig.collect {
            zipTree(it).matching {
                include 'checkstyle/*.xml'
                include 'license/*.txt'
            }
        }
    }
    into 'build'
}

task apidocs(type: Javadoc, dependsOn: getTasksByName('docs', true)) {
    outputs.upToDateWhen { false }
    destinationDir = new File(projectDir, "docs/apidocs")
    title = "Trellis Repository Documentation"
    exclude '**/impl/*'
    options.with {
        memberLevel = JavadocMemberLevel.PUBLIC
        links "http://docs.oracle.com/javase/8/docs/api/"
        links 'http://docs.oracle.com/javaee/7/api/'
        links 'https://kafka.apache.org/0110/javadoc/'
        links 'http://curator.apache.org/apidocs/'
        links 'https://trellis-ldp.github.io/trellis/apidocs/'
        links 'https://commons.apache.org/proper/commons-rdf/apidocs/'
    }

    source subprojects
        .findAll {!it.name.equals('trellis-app') && !it.name.equals('trellis-rosid-file-streaming') }
        .collect {project -> project.sourceSets.main.allJava }
    classpath = files(subprojects.collect {project -> project.sourceSets.main.compileClasspath})
}

checkstyleMain.dependsOn processBuildTools
